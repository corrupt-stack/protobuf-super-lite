# Copyright 2022 Yuri Wiitala. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

declare_args() {
  is_debug = false

  # Set to true to test the build with the GNU C++ toolchain. If false, the LLVM
  # toolchain is used instead.
  use_gcc_instead_of_llvm = false
}

# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [
  "//build:compiler_defaults",
  "//build:high_coding_standards",
  "//build:no_exceptions",
  "//build:no_rtti",
  "//build:symbol_visibility_hidden",
  "//build:default_sanitizers",
  "//build:default_include_dirs",
]

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = _shared_binary_target_configs
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}

if (use_gcc_instead_of_llvm) {
  set_default_toolchain("//build/toolchain:gcc")
} else {
  set_default_toolchain("//build/toolchain:llvm_on_posix")
}
